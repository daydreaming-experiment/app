buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 16
    buildToolsVersion "20.0.0"

    defaultConfig {
        versionCode 47
        versionName "0.25.0"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "+debug"
        }

        qa.initWith(buildTypes.debug)
        qa {
            applicationIdSuffix ".qa"
            versionNameSuffix "+qa"
        }

        applicationVariants.all { variant ->
            def file = variant.outputFile
            variant.outputFile = new File(file.parent,
                                          file.name.replace(variant.buildType.name + ".apk",
                                                            defaultConfig.versionName + "+" + variant.buildType.name + ".apk"))
        }
    }

    // Fix for duplicate files copied
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        // Lint raises InvalidPackage errors for packages we don't use and can't fix
        disable 'InvalidPackage'
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    compile('com.fasterxml.jackson.module:jackson-module-guice:2.4.1') {
        exclude group: 'com.google.inject', module: 'guice'
    }
    compile 'org.apache.httpcomponents:httpmime:4.3.4'
    compile 'com.madgag:scprov-jdk15on:1.47.0.3'
    // roboguice is guice for Android
    compile 'org.roboguice:roboguice:2.0'
    compile('com.google.inject.extensions:guice-assistedinject:3.0') {
        exclude group: 'com.google.inject', module: 'guice'
    }
    compile 'ch.acra:acra:4.5.0'
    compile 'com.google.android:annotations:4.1.1.4'
    compile project(':library')
}
